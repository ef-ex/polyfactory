vector getNewDir( vector prevDir, newDir; int vf, npt; string direction){
    vector dirvec = set(0,0,0);
    int npts[] = nearpoints(vf, point(vf, "P", npt), 20);

    foreach(int pt; npts){
        dirvec += point(vf, direction, pt)*point(vf, "weight", pt);
    }
    dirvec = normalize(dirvec);
    if (dot(prevDir, newDir)>=0){
        return dirvec;
    }
    else{
        return dirvec*-1;
    }
}

int drawStreet( int vf, steps; vector pos; float stepSize ; string dir){
    int pt, prim, npt;
    string type;
    vector prevDir, newDir;
    
    pt = addpoint(0, pos);
    prim = addprim(0, "polyline");
    addvertex(0, prim, pt);
    npt = nearpoint(vf, pos, 100);
    prevDir = point(vf, dir, npt);
    
    for (int i=0; i<steps; i++){

        npt = nearpoint(vf, pos, 100);
        newDir = getNewDir(prevDir, point(vf, dir, npt), vf, npt, dir);
        pos = pos+newDir*stepSize;
        pt = addpoint(0, pos);
        addvertex(0, prim, pt);
        prevDir = newDir;

    }
    return prim;
}
